import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath deps.plugins.kotlin
        classpath deps.plugins.dokka
        classpath deps.plugins.publish
        classpath deps.plugins.spotless
    }
}

apply plugin: "com.diffplug.spotless"

apply from: "$rootDir/gradle/dependencies.gradle"

spotless {
    kotlin {
        target "**/*.kt"
        //targetExclude "**/gen/**/*.*", "**/generated/**/*.*", "sqldelight-compiler/integration-tests/src/test/kotlin/com/example/**/*.*"
        trimTrailingWhitespace()
        endWithNewline()
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events = ["failed", "skipped", "passed"]
            exceptionFormat "full"
        }
    }

    plugins.withId("org.jetbrains.kotlin.jvm") {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    group = GROUP
    version = VERSION_NAME
}
